// See https://www.rfc-editor.org/rfc/rfc5322#section-3.4

WSP = _{ " " | "\t" } // todo: such alias slow down the parser
CRLF = _{ "\r\n" }

// 3.2.1

VCHAR = _{ '\x21'..'\x7E' }
quoted_pair = { "\\" ~ (VCHAR | WSP) }

// 3.2.2

FWS = _{ (WSP* ~ CRLF)? ~ WSP+ }
CTEXT = _{ '!'..'\'' | '*'..'[' | ']'..'~' }
ctext_seq = { CTEXT+ }
ccontent = _{ ctext_seq /*| quoted_pair*/ | comment }
comment = { "(" ~ (FWS? ~ ccontent)* ~ FWS? ~ ")" }
CFWS = _{ ((FWS? ~ comment)+ ~ FWS?) | FWS }

// 3.2.3

ATEXT = _{ ASCII_ALPHANUMERIC | "!" | "#" | "$" | "%" | "&" | "'" | "*" | "+" | "-" | "/" | "=" | "?" | "^" | "_" | "`" | "{" | "|" | "}" | "~" }
atext_seq = { ATEXT+ }
atom = _{ CFWS? ~ atext_seq ~ CFWS? }
dot_atom_text = { atext_seq ~ ("." ~ atext_seq)* }
dot_atom = _{ CFWS? ~ dot_atom_text ~ CFWS? }
specials = _{ "(" | ")" | "<" | ">" | "[" | "]" | ":" | ";" | "@" | "\\" | "," | "." | "\"" }
