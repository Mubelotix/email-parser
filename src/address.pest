// See https://www.rfc-editor.org/rfc/rfc5322#section-3.4

WSP = _{ " " | "\t" } // todo: such alias slow down the parser
CRLF = _{ "\r\n" }

// 3.2.  Lexical Tokens

// 3.2.1.    Quoted characters

VCHAR = _{ '\x21'..'\x7E' }
vchar_seq = { VCHAR+ }
quoted_pair = { "\\" ~ (VCHAR | WSP) }

// 3.2.2.    Folding White Space and Comments

FWS = _{ (WSP* ~ CRLF)? ~ WSP+ }
CTEXT = _{ '!'..'\'' | '*'..'[' | ']'..'~' }
ctext_seq = { CTEXT+ }
ccontent = _{ ctext_seq | quoted_pair | comment }
comment = { "(" ~ (FWS? ~ ccontent)* ~ FWS? ~ ")" }
CFWS = _{ ((FWS? ~ comment)+ ~ FWS?) | FWS }

// 3.2.3.    Atom

ATEXT = _{ ASCII_ALPHANUMERIC | "!" | "#" | "$" | "%" | "&" | "'" | "*" | "+" | "-" | "/" | "=" | "?" | "^" | "_" | "`" | "{" | "|" | "}" | "~" }
atext_seq = { ATEXT+ }
atom = _{ CFWS? ~ atext_seq ~ CFWS? }
dot_atom_text = { atext_seq ~ ("." ~ atext_seq)* }
dot_atom = _{ CFWS? ~ dot_atom_text ~ CFWS? }
specials = _{ "(" | ")" | "<" | ">" | "[" | "]" | ":" | ";" | "@" | "\\" | "," | "." | "\"" }

// 3.2.4.    Quoted Strings

QTEXT = _{ "!" | '#'..'[' | ']'..'~' }
qtext_seq = { QTEXT+ }
quoted_string = { CFWS? ~ "\"" ~ (FWS? ~ (qtext_seq | quoted_pair))* ~ FWS? ~ "\"" ~ CFWS? }

// 3.2.5.    Miscellaneous Tokens

word = { atom | quoted_string }
phrase = { word+ }
unstructured = { (FWS? ~ vchar_seq)* ~ WSP*}

// 3.3.  Date and Time Specification

date_time = { (day_of_week ~ ",")? ~ date ~ time ~ CFWS? }
day_of_week = _{ FWS? ~ day_name }
day_name = { "Mon" | "Tue" | "Wed" | "Thu" | "Fri" | "Sat" | "Sun" }
date = _{ (FWS? ~ day ~ FWS) ~ month ~ (FWS ~ year ~ FWS) } 
day = { ASCII_DIGIT{1,2} } // In the spec FWS are included in day, year and zone but I moved them one level up
month = { "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec" }
year = { ASCII_DIGIT{4} }
time = _{ time_of_day ~ (FWS ~ zone) }
time_of_day = _{ hour ~ ":" ~ minute ~ (":" ~ second)? }
hour = { ASCII_DIGIT{2} }
minute = { ASCII_DIGIT{2} }
second = { ASCII_DIGIT{2} }
zone = { ("+" | "-") ~ ASCII_DIGIT{4} }
